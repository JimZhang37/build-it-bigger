// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    repositories {
        jcenter()
        google()
    }
    dependencies {
        classpath 'com.google.guava:guava:24.1-jre'
        classpath 'com.android.tools.build:gradle:3.5.2'

        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

allprojects {
    repositories {
        jcenter()
        google()
    }
}

task runIntegrationTest {
    group = "integration test"
    description = "run an integration test from end to end"
    dependsOn = ["startBackendServer", "stopBackendServer", "runUiTest"]
    doLast {
        println "run an integration test from end to end"
    }
}

task startBackendServer {
    group = "integration test"
    description = "start app engine as backend server"
    dependsOn ':backend:appengineRun'
    evaluationDependsOn(":backend")
    doLast {
        println "start app engine as backend server"
    }

}

task stopBackendServer {
    group = "integration test"
    description = "stop app engine as backend server"
    dependsOn ':backend:appengineStop'
    mustRunAfter "runUiTest"
    doLast {
        println "stop app engine as backend server"
    }
}

task runUiTest {
    dependsOn ':app:connectedAndroidTest'
    mustRunAfter "startBackendServer"
    finalizedBy "stopBackendServer"
    group = "integration test"
    description = "in the UI test, the MainActivity's button is clicked, leading to fetch a joke from backend server and start a new activity with the joke"

    doLast {
        println "in the UI test, the MainActivity's button is clicked, leading to fetch a joke from backend server and start a new activity with the joke"
    }
}